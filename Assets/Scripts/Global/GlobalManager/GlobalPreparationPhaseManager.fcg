/* 
Global Preparation Phase Manager
Handles all the neccessary actions in Before Combat phase, including setting invincibility and move status for players,
together with teleport players to their designated starting positions.
*/
import "StdLibrary.fcc" as StdLib
import "EditorGenLib.fcc" as EditorLib
import "Player.fcc" as Player
import "Workflow.fcc" as Workflow
import "../GlobalData/PlayersTimerUI.fcg" as PlayersTimerUI
import "Collection.fcc" as Collection
import "../GlobalData/SpawnPoints.fcg" as SpawnPoints
import "Math.fcc" as Math
import "Strings.fcc" as Strings
import "List.fcc" as List
import "../GlobalData/PlayersShopIcon.fcg" as PlayersShopIcon
import "../GlobalManager/GlobalGameManager.fcg" as GlobalGameManager
import "Items.fcc" as Items
import "../GlobalData/CombatRoundCounter.fcg" as CombatRoundCounter
import "../../Player/PlayerController.fcg" as PlayerController
import "../GlobalManager/GlobalUIManager.fcg" as GlobalUIManager
import "../GlobalData/TeamData.fcg" as TeamData
import "../GlobalData/GlobalSafeZones.fcg" as GlobalSafeZones

graph GlobalPreparationPhaseManager {
    // Script variable storing the old stage index
    oldStageIndex int

    event OnPhaseStart(phase entity<Phase>) {
        if phase<Phase>.Index == EPhase.__Before6Combat__ {
            // Before combat, get a random sage index
            var randomStageIndex = RandomStage(oldStageIndex)

            // Memorize the old stage index 
            oldStageIndex = randomStageIndex
            // LogInfo("<GlobalPreparationPhaseManager.fcg>: Starting Preparation Phase. Init necessary data...") // Debug Info

            // Initialize data before combat
            wait InitDataBeforeCombat(phase<Phase>.Duration, randomStageIndex); 
        }
    }

    event OnPhaseEnd(phase entity<Phase>) {
        if phase<Phase>.Index == EPhase.__Before6Combat__{
            wait globalEntity<GlobalUIManager>.CreateRoundInfoHUD(); // Create the round info HUD for all players
        }
    }
    // UTILS 
    /* Initialize data before combat
    This function sets up all necessary data and states for players before the combat phase begins.
    More detail inside the function implementation.
    @param duration: The duration of Before Combat phase
    @param randomStageIndex: The random stage index for the current round
    */
    async func InitDataBeforeCombat(duration int, randomStageIndex int) {
        // LogError(globalEntity<TeamData>.GetSortedTeamList()); // Debug Info
        // Enable a tooltip for user to know how to get the full scoreboard
        start globalEntity<GlobalUIManager>.EnableTooltips();
        if globalEntity<CombatRoundCounter>.GetCount() == 0 {
            globalEntity<GlobalUIManager>.UpdatePlayersInScoreBoard(GetAllTeams()); // Set up the full scoreboard UI for all players
        }   
        // Initialize player states
        for index, player in GetAllPlayers() {
            player<PlayerController>.SetZeroGravityStatus(player, false); // Set back the gravity for the player
            globalEntity<PlayersTimerUI>.SetPlayerTimer(duration / 1000); // Set the player's timer
            globalEntity<PlayersTimerUI>.SetTimerVisibility(true); // Set the timer visibility to true
            SetInvincibilityStatus(player, true); // Set the invincibility status for the player
            // player<PlayerController>.AddMedKitEachRound(player); // Add Med Kit in each round
        }

        globalEntity<PlayersShopIcon>.ToggleAllIcons(true) // Show all players' shop icons
        InitPlayerWeapons(); // Initialize player weapons
        globalEntity<CombatRoundCounter>.IncreaseCount(); // Increment the round counter
        LogWarning("Random Stage Index: " + globalEntity<GlobalGameManager>.availableStages + " - " + randomStageIndex); // Debug Info
        var spawnPositions = globalEntity<SpawnPoints>.GetSpawnPositionForRound(randomStageIndex) // Get all spawn positions from the scene
        globalEntity<SpawnPoints>.SetSpawnPointsActiveStatusForRound(randomStageIndex, true); // Activate spawn points of the current stage

        for index, team in GetAllTeams() {
            var randomIndex = RandomInt(0, List.Length(spawnPositions)) // Get a random index in spawn list
            var randomPosition = spawnPositions[randomIndex] // Get the random spawn position
            // Use a loop to make sure all players in the team are teleported to the same position
            for index, player in team<Team>.AllTeammates {
                ReviveAtNextFrame(player) // Revive the player at the next frame
                Teleport(player, randomPosition, Vector3{0, 0, 0}) // Teleport the player to the random position
            }
            RemoveAt(spawnPositions, randomIndex) // Remove the used spawn position from the list
        }

        WaitForMillisecond(duration) // Wait for the specified duration

        globalEntity<PlayersTimerUI>.SetTimerVisibility(false); // Hide the timer UI after the preparation phase
        globalEntity<GlobalSafeZones>.OpenSafeZone(randomStageIndex); // Open the safe zone for the current round

        // When the round starts, reset player states
        for index, player in GetAllPlayers() {
            player<Player>.EP = 200; // Set player's initial EP to 200 every round
            SetInvincibilityStatus(player, false); // Disable invincibility for all players
            SetInvincibility(player, 5); // 5 secs of invincibility
        }
        globalEntity<PlayersShopIcon>.ToggleAllIcons(false); // Hide all players' shop icons
        globalEntity<SpawnPoints>.SetSpawnPointsActiveStatusForRound(randomStageIndex, false); // Disable all spawn points
    }

    /* Set Invincibility Status for a player (NO DURATION, TO SET INVINCIBLE WITH DURATION USE SetInvincibility() from Player.fcc library)
    @param player The player entity
    @param invincible The invincibility status to set
    */
    func SetInvincibilityStatus(player entity<Player>, invincible bool) {
        player<Player>.Invincible = invincible;
    }

    /* Set Movement Status for all players
    @param canMove The movement status to set
    */
    func SetMovementStatus(canMove bool) {
        SetAllPlayersMoveStatus(canMove);
    }

    // Add a M590 and a random rifle for each player
    func InitPlayerWeapons() {
        var rifleList = List<EResItem>{EResItem.AK47, EResItem.SCAR, EResItem.FAMAS, EResItem.XM8, EResItem.GROZAX, EResItem.AUG, EResItem.M4A1}
        var randomRifle = rifleList[RandomInt(0, List.Length(rifleList))]
        var m590 = EResItem.M590
        for index, player in GetAllPlayers(){
            // Add a list of items to player. Before add, delete all existing items
            player<PlayerController>.AddItemToPlayer(player, EResItem.VestLv3, 1)
            player<PlayerController>.AddItemToPlayer(player, EResItem.HelmetLv3, 1)
            player<PlayerController>.AddItemToPlayer(player, EResItem.Muzzle, 1)
            player<PlayerController>.AddItemToPlayer(player, EResItem.Mag, 1)
            player<PlayerController>.AddItemToPlayer(player, EResItem.Foregrip, 1)
            player<PlayerController>.AddItemToPlayer(player, EResItem.Scope, 1)
            player<PlayerController>.AddItemToPlayer(player, EResItem.Stock, 1)
            if GetItemCount(player, EResItem.GlooWall as ItemIDType) != 0 && globalEntity<CombatRoundCounter>.GetCount() != 0 {
                DestroyItem(player, EResItem.GlooWall as ItemIDType, GetItemCount(player, EResItem.GlooWall as ItemIDType)) // Destroy the item when added
            }
            // player<PlayerController>.AddItemToPlayer(player, EResItem.GlooWall, 8)
            AddItem(player, EResItem.GlooWall as ItemIDType, 8, out var newGlooWall) // Add the item to the player
            if GetItemCount(player, EResItem.MedKit as ItemIDType) != 0 {
                DestroyItem(player, EResItem.MedKit as ItemIDType, GetItemCount(player, EResItem.MedKit as ItemIDType)) // Destroy the item when added
            }
            AddItem(player, EResItem.MedKit as ItemIDType, 4, out var newItem) // Add the item to the player
            if GetItemCount(player, EResItem.SuperMed as ItemIDType) != 0 {
                DestroyItem(player, EResItem.SuperMed as ItemIDType, GetItemCount(player, EResItem.SuperMed as ItemIDType)) // Destroy the item when added
            }
            AddItem(player, EResItem.SuperMed as ItemIDType, 1, out var newSuperMed) // Add the item to the player
            if GetItemCount(player, EResItem.Inhaler as ItemIDType) != 0 {
                DestroyItem(player, EResItem.Inhaler as ItemIDType, GetItemCount(player, EResItem.Inhaler as ItemIDType)) // Destroy the item when added
            }
            AddItem(player, EResItem.Inhaler as ItemIDType, 10, out var newInhaler) // Add the item to the player
            if GetItemCount(player, EResItem.Grenade as ItemIDType) != 0 {
                DestroyItem(player, EResItem.Grenade as ItemIDType, GetItemCount(player, EResItem.Grenade as ItemIDType)) // Destroy the item when added
            }
            AddItem(player, EResItem.Grenade as ItemIDType, 2, out var newGrenade) // Add the item to the player
            if GetItemCount(player, EResItem.FlashFreeze as ItemIDType) != 0 {
                DestroyItem(player, EResItem.FlashFreeze as ItemIDType, GetItemCount(player, EResItem.FlashFreeze as ItemIDType)) // Destroy the item when added
            }
            AddItem(player, EResItem.FlashFreeze as ItemIDType, 1, out var newFlashFreeze) // Add the item to the player
            if GetItemCount(player, EResItem.GlooMelter as ItemIDType) != 0 {
                DestroyItem(player, EResItem.GlooMelter as ItemIDType, GetItemCount(player, EResItem.GlooMelter as ItemIDType)) // Destroy the item when added
            }
            AddItem(player, EResItem.GlooMelter as ItemIDType, 2, out var newGlooMelter) // Add the item to the player
            if(globalEntity<CombatRoundCounter>.GetCount() == 0) {
                player<PlayerController>.AddItemToPlayer(player, m590, 1) // Add M590 to the player
                player<PlayerController>.AddItemToPlayer(player, randomRifle, 1) // Add random rifle to the player
            }
        }
    }

    /*
    As the RandomInt() random mechanism is kinda bad (high chance of getting the same value as the old value every call), keep it inside a loop so that we can ensure a different value is returned.
    However, to be safe, limit the random time to 20
    @param oldValue: the old stage index
    @return: the new stage index
    */
    func RandomStage(oldValue int) int{
        var attempts = 0
        while attempts < 20 {
            // Randomize the new stage index
            var newValue = RandomInt(0, globalEntity<GlobalGameManager>.availableStages)

            // Stop condition: When the new value is the same as the old value
            if newValue != oldValue {
                return newValue
            }

            // Increase attempts if the new value is the same as the old value
            attempts++
        }
        return oldValue // After 20 attempts, return the old value if no new value is found
    }
}